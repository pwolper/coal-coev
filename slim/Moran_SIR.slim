// set up a simple neutral simulation
initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(keepPedigrees=T);
	initializeTreeSeq();
	initializeMutationRate(0);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 0);
	initializeRecombinationRate(0);

	defineConstant("simTime", 20000);
	defineConstant("treeSeq", F);

	defineConstant("N", 300);

	defineConstant("SUSCEPTIBLE", 0);
	defineConstant("RESISTANT", 1);
	defineConstant("INFECTED", 2);
	
	defineConstant("S_s", 0.1); //Reproductive advantage of susceptible (vs. infected)
	defineConstant("S_r", 0.05); //Reproductive advantage of resistant (vs. infected)
	defineConstant("c", 0.00); //Individual contact rate scale value by N?
	defineConstant("m_r", 0.0); //Mutation infected -> resistant
	defineConstant("m_s", 0.0); //Mutation resistant -> suscpetible
	defineConstant("m_i", 0.0); //Migration of infected individuals into system default= 0.005
}


reproduction() {
	// Moran reproduction. Sample one individual to reproduce
	fitness = p1.cachedFitness(NULL);
	parent = sample(p1.individuals, 1, weights=fitness);
	offspring = subpop.addCloned(parent);
	offspring.tagF = parent.tagF;
	offspring.tag = parent.tag;
	offspring.color = parent.color;
	self.active = 0;

}

1 first() {
	defineConstant("simID", getSeed());
	sim.addSubpop("p1", N, haploid = T);
	p1.individuals.tagF = asFloat(seqLen(p1.individualCount));
	p1.individuals.tag = SUSCEPTIBLE;
	p1.individuals.color = colors(p1.individualCount, "turbo");
	
	defineGlobal('countS', rep(0, simTime));
	defineGlobal('countI', rep(0, simTime));
	defineGlobal('countR', rep(0, simTime));

}

1: early() {
	inds = p1.individuals;

	//Infection with rate c
	for (ind in p1.individuals) {
		if (runif(1) < c) {
			target = sample(p1.individuals, 1);
			if (ind.tag == INFECTED | target.tag == INFECTED) {
				if (ind.tag != RESISTANT) {ind.tag = INFECTED;}
				if (target.tag != RESISTANT) {target.tag = INFECTED;}
			}
		}
	}
	
	// Mutation I->R
	for (infected in inds[inds.tag == INFECTED]) {
		if (runif(1) < m_r) {
			infected.tag = RESISTANT;
		}
	}
	
	//mutation R->S
	for (resistant in inds[inds.tag == RESISTANT]) {
		if (runif(1) < m_s) {
			resistant.tag = SUSCEPTIBLE;
		}
	}
	
	//migration infection S->I
	for (susceptible in inds[inds.tag == SUSCEPTIBLE]) {
		if (runif(1) < m_i) {
			susceptible.tag = INFECTED;
		}
	}
	
	//fitness recalculations
	sus = inds[inds.tag == SUSCEPTIBLE];
	res = inds[inds.tag == RESISTANT];
	sus.fitnessScaling = 1 + S_s;
	res.fitnessScaling = 1 + S_r;
	fitness = p1.cachedFitness(NULL);
}

1: late() {
	victim = sample(p1.individuals, 1);
	sim.killIndividuals(victim);
	fitness = p1.cachedFitness(NULL);

	//logging
	tags = p1.individuals.tag;
	countS[sim.cycle-1] = sum(tags == SUSCEPTIBLE);
	countR[sim.cycle-1] = sum(tags == RESISTANT);
	countI[sim.cycle-1] = sum(tags == INFECTED);
}

1 early() {
	//initial resistance
	resistant = p1.sampleIndividuals(asInteger(N/3), tag=SUSCEPTIBLE);
	resistant.tag = RESISTANT;
	//catn("Resistance introduced in Individual " + asInteger(resistant.tagF));
	
	//initial infection
	infected = p1.sampleIndividuals(asInteger(N/3), tag=SUSCEPTIBLE);
	infected.tag = INFECTED;
	//catn("Infection introduced in Individual " + asInteger(infected.tagF));

}

simTime late() {
	// catn(countS);
	// catn(countI);
	// catn(countR);
	inds = p1.individuals;
	susceptibles = inds[inds.tag == SUSCEPTIBLE];
	infected = inds[inds.tag == INFECTED];
	resistant = inds[inds.tag == RESISTANT];
	
	catn("Susceptible:");
	//catn(asInteger(susceptibles.tagF));
	//catn(tabulate(asInteger(susceptibles.tagF)));
	catn("Types: " + length(unique(asInteger(susceptibles.tagF))));
	typeCounts = unique(tabulate(asInteger(susceptibles.tagF)));
	catn("Type counts:");
	catn(typeCounts[typeCounts != 0]); //remove 0 from tabulate
	catn("\nInfected:");	
	catn("Types: " + length(unique(asInteger(infected.tagF))));
	typeCounts = unique(tabulate(asInteger(infected.tagF)));
	catn("Type counts:");
	catn(typeCounts[typeCounts != 0]); //remove 0 from tabulate
	//catn(asInteger(infected.tagF));
	catn("Resistant:");	
	catn("Types: " + length(unique(asInteger(resistant.tagF))));
	typeCounts = unique(tabulate(asInteger(resistant.tagF)));
	catn("Type counts:");
	catn(typeCounts[typeCounts != 0]); //remove 0 from tabulate	
	//catn(asInteger(resistant.tagF));
	
	
	//catn(asInteger(p1.individuals.tagF));
	//catn(p1.individuals.tag);


	if (exists("slimgui")) {
	x = seqLen(simTime);
	plot = slimgui.createPlot("SIR dynamics", xlab="Generation", ylab="Frequency", width=700, height=500);
	plot.lines(x, countS/N, 'green');
	plot.lines(x, countI/N, 'red');
	plot.lines(x, countR/N, 'cornflowerblue');
	plot.addLegend("topLeft", inset=0, labelSize=13);
	plot.legendLineEntry("susceptible", "green", lwd=2);
	plot.legendLineEntry("infected", "red", lwd=2);
	plot.legendLineEntry("resistant", "cornflowerblue", lwd=2);
	}

	if ("treeSeq" == T) {
		inds = sortBy(sim.subpopulations.individuals, "pedigreeID");
		infectionDict = Dictionary("ID", inds.pedigreeID, "InfectionTags", inds.tag);
		file = paste0("./trees/Moran_SIR_test.trees");
		sim.treeSeqOutput(file, metadata=infectionDict);
	}
}
